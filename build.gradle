buildscript {
    ext.with {
        hasGbArtifactory = (project.hasProperty('gbArtifactoryUrl')
                && project.hasProperty('gbArtifactoryUser')
                && project.hasProperty('gbArtifactoryPassword'))

        hasGbUploadArtifactory = (project.hasProperty('gbArtifactoryUploadUrl')
                && project.hasProperty('gbArtifactoryUser')
                && project.hasProperty('gbArtifactoryPassword'))

        setRepositoriesFor = { RepositoryHandler repositoryHandler ->
            if (hasGbArtifactory) {
                repositoryHandler.maven {
                    credentials {
                        username gbArtifactoryUser
                        password gbArtifactoryPassword
                    }
                    url gbArtifactoryUrl
                }
            }
            repositoryHandler.jcenter()
            repositoryHandler.maven { url 'https://jitpack.io' }
        }
        batmDependencySubstitutionConfig = file('dependencySubstitutions.txt')
        batmDependencyChecksumsConfig = file('dependencyChecksums.txt')
    }
    setRepositoriesFor(repositories)
}

plugins {
    id 'com.generalbytes.gradle.main'
    id 'pl.allegro.tech.build.axion-release' version '1.9.3'
}

scmVersion {
    tag {
        prefix = ''
    }
}

allprojects {
    setRepositoriesFor(buildscript.repositories)
    setRepositoriesFor(repositories)

    group = projectGroup
    version = scmVersion.version

    tasks.withType(JavaCompile) {
        project.sourceCompatibility = JavaVersion.VERSION_1_8
        project.targetCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
        options.incremental = true
    }

    afterEvaluate {
        if (pluginManager.hasPlugin('com.generalbytes.gradle.dependency.verification')) {
            dependencyChecksums {
                global = true
            }
            dependencyVerifications {
//                setConfigurations([]) // effectively turns off all checksum verifications
//                failOnChecksumError = false // turns off failure on checksum problem (only warnings are printed)
            }
        }
        if (pluginManager.hasPlugin('com.generalbytes.gradle.dependency.strict')) {
            strictDependencies {
//                conflictFail false // turns off failure on version conflict
            }
        }
    }
}

dependencyVerifications {
    checksums batmDependencyChecksumsConfig
    printUnusedAssertions false
}

/**
 * Basic release versioning workflow:
 *
 * $ git tag
 * project-0.1.0
 *
 * $ ./gradlew currentVersion
 * 0.1.0
 *
 * $ git commit -m "Some commit."
 *
 * $ ./gradlew currentVersion
 * 0.1.1-SNAPSHOT
 *
 * $ ./gradlew createRelease
 *
 * $ git tag
 * project-0.1.0 project-0.1.1
 *
 * $ git push --tags
 *
 * $ ./gradlew currentVersion
 * 0.1.1
 *
 * $ ./gradlew publish
 * published project-0.1.1 release version
 *
 * $ ./gradlew markNextVersion -Prelease.version=1.0.0
 *
 * $ ./gradlew currentVersion
 * 1.0.0-SNAPSHOT
 */
