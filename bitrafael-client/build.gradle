plugins {
    id 'java'
    id 'maven-publish'
    id 'com.generalbytes.gradle.main'
}

configurations {
    // handle Github (e.g. community) vs GENERAL BYTES dichotomy
    generalBytesCompile
    githubCompile
    if (hasGbArtifactory) {
        implementation { extendsFrom generalBytesCompile }
    } else {
        implementation { extendsFrom githubCompile }
    }
}

dependencies {
    implementation project(':bitrafael-server-api')
    implementation project(':bitrafael-client-api')
    implementation(group: 'com.madgag.spongycastle', name: 'core', version: '1.52.0.0')
    implementation(group: 'com.google.guava', name: 'guava', version: '31.1-jre')

    final String dashjVersion = '0.15.2.20190416' // https://github.com/GENERALBYTESCOM/dashj/tree/generalbytes
    final String litecoinjVersion = '0.15.1.20260115' // https://github.com/GENERALBYTESCOM/litecoinj/tree/litecoin-0.15.1

    generalBytesCompile(group: 'org.litecoinj', name: 'core', version: litecoinjVersion)
    generalBytesCompile(group: 'org.dashj', name: 'dashj-core', version: dashjVersion)

    githubCompile(group: 'com.github.GENERALBYTESCOM', name: 'litecoinj', version: litecoinjVersion)
    githubCompile(group: 'com.github.GENERALBYTESCOM.dashj', name: 'dashj-core', version: dashjVersion)

    implementation('org.bitcoinj:bitcoinj-core:0.16.5')
    implementation('org.bouncycastle:bcprov-jdk15to18:1.73')
    implementation(group: 'com.github.mmazi', name: 'rescu', version: '3.1') {
        // Keep SLF4J 1.7.32 used by other dependencies
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation(group: 'jakarta.ws.rs', name: 'jakarta.ws.rs-api', version: '3.1.0')
    implementation(group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.14.2')
    runtimeOnly(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36')
    runtimeOnly(group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36')

    testImplementation(group: 'junit', name: 'junit', version: '4.10')
}
dependencySubstitutions {
    substitute file(batmDependencySubstitutionConfig)
}

dependencyVerifications {
    checksums batmDependencyChecksumsConfig
    printUnusedAssertions false

    // additional configurations
    if (hasGbArtifactory) {
        configuration project.configurations.generalBytesCompile
    }
    configuration project.configurations.githubCompile
}

publishing {
    if (hasGbUploadArtifactory) {
        repositories {
            maven {
                credentials {
                    username gbArtifactoryUser
                    password gbArtifactoryPassword
                }
                url gbArtifactoryUploadUrl
            }
        }
    }
    
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
