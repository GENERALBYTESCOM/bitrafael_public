plugins {
    id 'java'
    id 'maven-publish'
    id 'com.generalbytes.gradle.main'
}

configurations {
    // handle Github (e.g. community) vs GENERAL BYTES dichotomy
    generalBytesCompile
    githubCompile
    if (hasGbArtifactory) {
        compile { extendsFrom generalBytesCompile }
    } else {
        compile { extendsFrom githubCompile }

    }
}

dependencies {
    compile project(':bitrafael-common')
    compile(group: 'com.madgag.spongycastle', name: 'core', version: '1.52.0.0')
    compile(group: 'com.google.guava', name: 'guava', version: '27.0.1-android')

    final String dashjVersion = '0.15.2.20190416' // https://github.com/GENERALBYTESCOM/dashj/tree/generalbytes
    final String litecoinjVersion = '0.15.1.20190418' // https://github.com/GENERALBYTESCOM/litecoinj/tree/litecoin-0.15.1
    final String bitcoinjVersion = '0.15.1.20190411' // https://github.com/GENERALBYTESCOM/bitcoinj/tree/master

    generalBytesCompile(group: 'org.bitcoinj', name: 'core', version: bitcoinjVersion)
    generalBytesCompile(group: 'org.litecoinj', name: 'core', version: litecoinjVersion)
    generalBytesCompile(group: 'org.dashj', name: 'dashj-core', version: dashjVersion)

    githubCompile(group: 'com.github.GENERALBYTESCOM', name: 'bitcoinj', version: bitcoinjVersion)
    githubCompile(group: 'com.github.GENERALBYTESCOM', name: 'litecoinj', version: litecoinjVersion)
    githubCompile(group: 'com.github.GENERALBYTESCOM.dashj', name: 'dashj-core', version: dashjVersion)

    compile 'org.bouncycastle:bcprov-jdk15on:1.60' // bitcoinj depends on this and exposes it
    compile(group: 'com.github.mmazi', name: 'rescu', version: '2.0.2')
    runtime(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25')
    runtime(group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25')

    testCompile(group: 'junit', name: 'junit', version: '4.10')
}
dependencySubstitutions {
    substitute file(batmDependencySubstitutionConfig)
}

dependencyVerifications {
    checksums batmDependencyChecksumsConfig
    printUnusedAssertions false

    // additional configurations
    if (hasGbArtifactory) {
        configuration project.configurations.generalBytesCompile
    }
    configuration project.configurations.githubCompile
}

publishing {
    if (hasGbUploadArtifactory) {
        repositories {
            maven {
                credentials {
                    username gbArtifactoryUser
                    password gbArtifactoryPassword
                }
                url gbArtifactoryUploadUrl
            }
        }
    }
    
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
